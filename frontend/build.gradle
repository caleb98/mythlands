plugins {
    id "com.github.node-gradle.node" version "3.3.0"
}

def buildDev = tasks.register('buildDev', NpmTask) {
    description 'Uses npm run build in development mode.'
    args = ['run', 'build', '--', '--mode', 'development']
}

def build = tasks.register('build', NpmTask) {
    description 'Uses npm run build in production mode.'
    args = ['run', 'build']
}

buildDev.configure {
    inputs.files fileTree('public')
    inputs.files fileTree('src')

    inputs.file 'package.json'
    inputs.file 'package-lock.json'

    outputs.dir 'dist'
}

build.configure {
    inputs.files fileTree('public')
    inputs.files fileTree('src')

    inputs.file 'package.json'
    inputs.file 'package-lock.json'

    outputs.dir 'dist'
}

configurations {
    staticResources {
        canBeConsumed = true
        canBeResolved = false
    }
}

artifacts {
    staticResources buildDev
    staticResources build
}